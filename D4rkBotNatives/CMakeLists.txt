cmake_minimum_required(VERSION 3.0)
project(mp3encoder C)

add_subdirectory(lame)

if("${CMAKE_SYSTEM_NAME}" STREQUAL "Windows")
	include_directories("$ENV{JAVA_HOME}/include/win32")
	set(SYSNAME "win")
elseif("${CMAKE_SYSTEM_NAME}" STREQUAL "Linux")
	include_directories("$ENV{JAVA_HOME}/include/linux")
	set(SYSNAME "linux")
else()
	include_directories("$ENV{JAVA_HOME}/include/darwin")
	set(SYSNAME "darwin")
endif()

if ("${CMAKE_C_COMPILER_ID}" STREQUAL "MSVC")
	add_definitions(-Dinline=__inline)

	set(COMPILER_FLAGS "")
	set(COMPILER_FLAGS_DEBUG "/MTd")
	set(COMPILER_FLAGS_RELEASE "/Ox /MT")
elseif("${CMAKE_SYSTEM_NAME}" MATCHES "Darwin")
	set(COMPILER_FLAGS "-fvisibility=hidden")
	set(COMPILER_FLAGS_DEBUG "-g")
	set(COMPILER_FLAGS_RELEASE "-O3 -DNDEBUG")
	set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Wl,-dead_strip")
else()
	set(COMPILER_FLAGS "-Wall -Wextra -Werror -Wno-unused-parameter -Wno-unused-function -fvisibility=hidden")
	set(COMPILER_FLAGS_DEBUG "-g")
	set(COMPILER_FLAGS_RELEASE "-O3 -DNDEBUG")
	set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Wl,--gc-sections -Wl,--exclude-libs,ALL -Wl,--strip-all")
endif()

set(CMAKE_C_FLAGS_DEBUG "${COMPILER_FLAGS} ${COMPILER_FLAGS_DEBUG}")
set(CMAKE_C_FLAGS_RELEASE "${COMPILER_FLAGS} ${COMPILER_FLAGS_RELEASE}")
set(CMAKE_CXX_FLAGS_DEBUG "${COMPILER_FLAGS} ${COMPILER_FLAGS_DEBUG}")
set(CMAKE_CXX_FLAGS_RELEASE "${COMPILER_FLAGS} ${COMPILER_FLAGS_RELEASE}")

include_directories("$ENV{JAVA_HOME}/include")
include_directories("./lame/lame-3.100/include")

if (DEFINED ENV{DIST_DIR})
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "$ENV{DIST_DIR}")
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE "$ENV{DIST_DIR}")
endif()

add_library(mp3encoder SHARED Mp3Encoder.c)

if (WIN32)
    target_link_libraries(mp3encoder lame)
else()
    target_link_libraries(mp3encoder lame m)
endif()